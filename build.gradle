plugins {
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.4' apply false
}

ext {
    set('springBootVersion', "3.5.4")
    set('springCloudVersion', "2025.0.0")
    set('mapstructVersion', "1.5.5.Final")
    set('jjwtVersion', "0.11.5")
    set('springdocVersion', "2.8.9")
    set('lombokVersion', "1.18.38")
}


allprojects {
    group = 'kt.aivle'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
            dependency "org.springdoc:springdoc-openapi-starter-webflux-ui:${springdocVersion}"
            dependency "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
            dependency "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
            dependency "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
            dependency "org.projectlombok:lombok:${lombokVersion}"
        }
    }
}